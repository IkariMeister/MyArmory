plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
}

android {

    compileSdkVersion compile_sdk

    defaultConfig {
        applicationId "com.jcgseco.myarmory"
        targetSdkVersion target_sdk
        minSdkVersion min_sdk
        versionCode app_version_code
        versionName app_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'app_name', 'My Armory'
    }

    buildTypes {
        dev {
            applicationIdSuffix '.dev'
            versionNameSuffix '_DEV'
            resValue 'string', 'app_name', 'My Armory Dev'
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'false'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'false'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_dev",
                    appIconRound: "@mipmap/ic_launcher_dev_round"
            ]
        }

        integration {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics { mappingFileUploadEnabled true }
            applicationIdSuffix '.int'
            versionNameSuffix '_INT'
            resValue 'string', 'app_name', 'My Armory Integration'
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'false'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_staging",
                    appIconRound: "@mipmap/ic_launcher_staging_round"
            ]
        }

        beta {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics { mappingFileUploadEnabled true }
            applicationIdSuffix '.beta'
            resValue 'string', 'app_name', 'My Armory Beta'
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'false'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_beta",
                    appIconRound: "@mipmap/ic_launcher_beta_round"
            ]
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics { mappingFileUploadEnabled true }
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'true'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
    }

    flavorDimensions "version"

    productFlavors {
        prod {
            dimension "version"
        }
        stg {
            dimension "version"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation project(':feat-base')

    // Canary leak must only run on debug builds
    debugImplementation libs.canary_leak

}
