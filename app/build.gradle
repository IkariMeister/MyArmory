plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
}

android {

    compileSdkVersion compile_sdk

    defaultConfig {
        targetSdkVersion target_sdk
        minSdkVersion min_sdk
        versionCode app_version_code
        versionName app_version
    }

    buildTypes {
        debug {
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'false'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'false'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher_dev",
                    appIconRound: "@mipmap/ic_launcher_dev_round"
            ]
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseCrashlytics { mappingFileUploadEnabled true }
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
            buildConfigField 'boolean', 'ENABLE_ANALYTICS_TRACKER', 'true'
            manifestPlaceholders = [
                    appIcon     : "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
    }

    flavorDimensions "version"

    productFlavors {
        prod {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix '.dev'
            versionNameSuffix '_DEV'
            resValue 'string', 'app_name', 'My Armory Dev'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation project(':feat-base')

    api libs.hilt_android
    kapt libs.hilt_compiler

    // Canary leak must only run on debug builds
    debugImplementation libs.canary_leak

}
